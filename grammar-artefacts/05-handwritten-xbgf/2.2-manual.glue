// Manually-written glue for version 2.7.2

// ------------------------------------------------------
// rename
// Transformations to do with renaming, creating and deleting non-terminals

// ------------------------------------------------------
// remove
// Narrowing conversion: remove eval_input and then (consequently) star_NEWLINE.


removeH start
	::= file_input
	| (<>: eval_input)
	| (<>: single_input)
	;
.

eliminate eval_input.
eliminate single_input.
eliminate star_NEWLINE.

// Problems with fpdef in varargslist:

// First deyaccify and inline  star_COMMA_fpdef
extract temp ::= "COMMA" fpdef opt_EQUAL_test; in star_COMMA_fpdef.
deyaccify star_COMMA_fpdef.
inline temp.
inline star_COMMA_fpdef.

// Now rework the comma leftwards:
massage (("COMMA" fpdef opt_EQUAL_test)*) to (("COMMA" fpdef opt_EQUAL_test)+ | ε) in varargslist.
distribute in varargslist.
extract temp ::= fpdef opt_EQUAL_test;  in varargslist.
massage (temp ("COMMA" temp)+) to ((temp "COMMA")+ temp) in varargslist.
inline temp.

// Now replace the + with a *
extract temp ::= (fpdef opt_EQUAL_test "COMMA")+; in varargslist.
factor (temp fpdef opt_EQUAL_test opt_COMMA | fpdef opt_EQUAL_test opt_COMMA) to ((temp | ε) fpdef opt_EQUAL_test opt_COMMA) in  varargslist.
inline temp.
massage ((fpdef opt_EQUAL_test "COMMA")+ | ε) to ((fpdef opt_EQUAL_test "COMMA")*) in varargslist.

// Last, extract this new use of star_fpdef_COMMA:
extract temp ::= fpdef opt_EQUAL_test "COMMA"; in star_fpdef_COMMA.
deyaccify star_fpdef_COMMA.
inline temp.
fold star_fpdef_COMMA in varargslist.
yaccify star_fpdef_COMMA ::= star_fpdef_COMMA (fpdef opt_EQUAL_test "COMMA") | ε;.



// 2.0 and 2.2 specific stuff
// Making a decicion to narrow the language here (twice),
// as this is what's in 2.3 and after...

// (1) Narrowing in definition of power (repeat -> option)

extract temp ::= "DOUBLESTAR" factor; in star_DOUBLESTAR_factor.
deyaccify star_DOUBLESTAR_factor.
inline temp.
inline star_DOUBLESTAR_factor.

narrow ("DOUBLESTAR" factor)* to ("DOUBLESTAR" factor)? in power.
massage (("DOUBLESTAR" factor)?) to (("DOUBLESTAR" factor) | ε) in power.
distribute in power.


// (2) Narrowing in definition of atom -> "BACKQUOTE" testlist "BACKQUOTE"
// introduce testlist1 : test | (star_COMMA_test); in place of testlist above.
// Change testlist to testlist1
unfold testlist in atom.
extract testlist1 ::= (test star_COMMA_test "COMMA" | test star_COMMA_test); in atom.
// The narrowing:
removeH testlist1
        ::= (<>: (test star_COMMA_test "COMMA"))
        | test (star_COMMA_test)
        ;.


